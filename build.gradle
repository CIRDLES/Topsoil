// BEGIN JAVASCRIPT SUPPORT
plugins {
    id 'com.eriwen.gradle.js' version '1.12.1'
    id 'com.github.rundis.buster' version '0.2.4.2'
}
// END JAVASCRIPT SUPPORT

apply plugin: 'java'

apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'maven'

import org.apache.tools.ant.filters.ReplaceTokens

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

ext.mainClass = 'org.cirdles.topsoil.app.Topsoil'

def configJavaExec = { JavaExec task ->
    task.main = mainClass
    task.classpath = sourceSets.main.runtimeClasspath
    task.standardInput = System.in
}

task run(dependsOn: project.classes, type: JavaExec) {
    configJavaExec(it)
}

task debug(dependsOn: project.classes, type: JavaExec) {
    configJavaExec(it)
    debug = true
}

checkstyle {
    toolVersion = '6.6'
}

// BEGIN BETTER VERSIONING
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()

    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }

    return stdout.toString().trim()
}

processResources {
    from('src/main/resources') {
        include '**/*.properties'
        filter(ReplaceTokens, tokens: [version: getVersionName()])
    }
}
// END BETTER VERSIONING

task jarWithDependencies(type: Jar) {
    manifest {
        attributes 'Main-Class': mainClass
    }

    baseName = project.name + '-' + getVersionName()
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

test {
    systemProperty 'java.awt.headless', true
    systemProperty 'prism.order', 'sw'
    systemProperty 'prism.text', 't2k'
    systemProperty 'testfx.headless', true
    systemProperty 'testfx.robot', 'glass'
    systemProperty 'testfx.setup.timeout', 2500

    useJUnit {
        if (System.getProperty('os.name').startsWith('Mac OS')) {
            excludeCategories 'org.cirdles.OSXIncompatible'
        }
    }
}

// BEGIN JAVASCRIPT SUPPORT
javascript.source {
    topsoil {
        js {
            srcDir 'src/main/resources/org/cirdles/topsoil/chart/topsoil_js'
            include '*.js'
        }
    }
}

combineJs {
    source = javascript.source.topsoil.js.files
    dest = file("${project.buildDir}/resources/main/org/cirdles/topsoil/chart/topsoil.js")
}

processResources.dependsOn combineJs

buster {
    configFile = file('config/buster.js')
}
// END JAVASCRIPT SUPPORT

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

repositories {
    jcenter()
    mavenLocal()

    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    compile 'org.controlsfx:controlsfx:8.20.8'
    compile 'net.sf.opencsv:opencsv:2.3'
    compile 'com.github.cirdles:commons:3214847'
    compile 'com.github.johnzeringue:extendsfx:1.0.0-alpha.1'
    compile 'com.google.inject:guice:4.0'
    compile 'com.google.inject.extensions:guice-multibindings:4.0'
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'org.mybatis:mybatis:3.3.0'
    compile 'org.mybatis:mybatis-guice:3.7'
    compile 'org.xerial:sqlite-jdbc:3.8.11.1'
    compile 'org.flywaydb:flyway-core:3.2.1';
    compile 'org.springframework:spring-jdbc:4.2.0.RELEASE'
    compile 'com.google.code.findbugs:jsr305:3.0.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.31-beta'
    testCompile 'com.google.jimfs:jimfs:1.0'
    testCompile 'org.testfx:testfx-core:4.0.+'
    testCompile 'org.testfx:testfx-junit:4.0.+'
    testCompile 'org.testfx:openjfx-monocle:1.8.0_20'
    testCompile 'com.google.code.findbugs:annotations:3.0.0'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}
